100 'RTCSET - set RTC from BP/BP+
101 'main variables
102 'A=Date, B=Time, C=Day of Week, E/E() BackPack status
103 'O=order in date string, 123=DMY 213=MDY 321=YMD
200 'program setup
201 'generic
211 MAXFILES=2:DEFSTRA-F:DIMA(3),E(3):F="COM:98N1ENN"
220 'NECs need other COM setting
221 IFPEEK(1)=148THENF="COM:9N81XN"
300 'open COM for I/O
301 OPENFFOROUTPUTAS1
302 OPENFFORINPUTAS2
400 'main program
401 CLS:PRINT"RTCSET - set RTC from BP/BP+"
410 'call BP for RTC data
411 PRINT#1,"FF";CHR$(7):INPUT#2,A,B,C
420 'set BP to CLI mode
421 PRINT#1,STRING$(5,13)
422 E=INPUT$(1,2):IFE<>" "GOTO422
423 FORT=0TO150:NEXT
430 'call BP settings and store in array
431 PRINT#1,"SET":E=INPUT$(3,2):PRINT#1,CHR$(13)
432 FORI=0TO3:LINEINPUT#2,E(I):NEXT:CLOSE
440 'BP date format->numeric
441 O=ASC(MID$(E(1),16,1))
442 O=-(O=68)*123-(O=77)*213-(O=89)*321:
450 'normalize BP DATE string
451 IFO=321THENA=MID$(A,3,8)ELSEA=LEFT$(A,6)+RIGHT$(A,2)
500 'build device DATE string
510 'split BP DATE string to DD,MM,YY
511 FORI=7TO1STEP-3:P=OMOD10:A(P)=MID$(A,I,2):O=O\10:NEXT
520 'get DATE format of device
521 ONERRORGOTO701
522 DATE$=D
530 'assemble device DATE string
531 FORI=3TO1STEP-1:P=OMOD10:A=A(P)+"/"+A:O=O\10:NEXT
600 'set DATE, TIME, DAY
610 'DATE
611 DATE$=LEFT$(A,8)
620 'TIME in 24H format
621 TIME$=RIGHT$("0"+MID$(STR$(VAL(B)+(RIGHT$(B,1)="P"AND12)),2,2)+RIGHT$(LEFT$(B,LEN(B)-1),6),8)
630 'DAY
631 DAY$=LEFT$(C,3)
640 'show data set
641 PRINT"RTC set to",DATE$,TIME$,DAY$
699 END
700 'read DATE pattern to be checked
701 READD,O
702 RESUME
800 'DATE pattern US, INT, JPN
801 DATA"31/12/99",123,"12/31/99",213,"99/12/31",321
